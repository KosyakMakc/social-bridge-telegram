plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm' version '2.2.20'
}

group = 'io.github.kosyakmakc'
version = System.getenv("VERSION") ?: '0.1.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = "tgbridge"
        url = "https://mvn.vtp.sh/main"
    }
    maven {
        name = "gitea"
        url = "https://git.kosyakmakc.ru/api/packages/kosyakmakc/maven"
    }
}

dependencies {
    compileOnly "dev.vanutp.tgbridge:common:0.1-SNAPSHOT"
    compileOnly "com.j256.ormlite:ormlite-core:6.1"
    compileOnly "com.j256.ormlite:ormlite-jdbc:6.1"
    compileOnly "io.github.kosyakmakc:social-bridge:0.1.0-13"

    testImplementation "com.j256.ormlite:ormlite-core:6.1"
    testImplementation "com.j256.ormlite:ormlite-jdbc:6.1"
    testImplementation "dev.vanutp.tgbridge:common:0.1-SNAPSHOT"
    testImplementation "io.github.kosyakmakc:social-bridge:0.1.0-13"

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

test {
    useJUnitPlatform()
}

publishing {
    // other settings of publication
    publishing {
        publications {
            mavenJava(MavenPublication) {
                groupId = project.group.toString()
                artifactId = project.name
                version = System.getenv("VERSION") ?: project.version.toString()
                from(components["java"])
            }
        }
    }
    repositories {
        maven {
            name = "Gitea"
            url = uri("https://git.kosyakmakc.ru/api/packages/kosyakmakc/maven")

            credentials(HttpHeaderCredentials) {
                name = "Authorization"
                value = "token " + System.getenv("REGISTRY_TOKEN")
            }

            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}